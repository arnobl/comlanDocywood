grammar fr.obeo.releng.targetplatform.TargetPlatform with org.eclipse.xtext.common.Terminals

import "src/main/resources/tpd/targetplatform.ecore"
import "http://www.eclipse.org/emf/2002/Ecore" as ecore
											
TargetPlatform:
	//(
		'target' name=STRING
    contents+=Options*
     contents+=Environment*
      contents+=IncludeDeclaration*
       contents+=Location*
//	)?
;

Options:
	'with' options+=Option (','? options+=Option)*
;

Environment:
	'environment' env+=ID (','? env+=ID)*
;

IncludeDeclaration:
	'include' importURI=STRING
;

Location:
	'location' ((ID=ID)? & uri=STRING) ('{'
		('with' options+=Option (','? options+=Option)*)?
		ius+=IU*
	'}')?
;

enum Option:
	INCLUDE_REQUIRED |
	INCLUDE_ALL_ENVIRONMENTS |
	INCLUDE_SOURCE |
	INCLUDE_CONFIGURE_PHASE
;

IU:
	ID=ID ( (';' 'version' '=')? (version=STRING) )?
;

